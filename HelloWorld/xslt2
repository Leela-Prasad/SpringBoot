<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:xs="http://www.w3.org/2001/XMLSchema"
	exclude-result-prefixes="#all"
	version="3.0">

  <xsl:param name="doc2">
<resources>
    <resource name="resource1">
        <codes>
            <code id="1" field1="value71" field2="value72" override="true" />
            <code id="2" field1="value81" field2="value82" override="true" />
            <code id="4" field1="value91" field2="value92" override="true" /> 
        </codes>
    </resource>
    <resource name="resource3">
        <codes>
            <code id="2" field1="value81" field2="value82" override="true" />
        </codes>
    </resource>
</resources>
  </xsl:param>
  
  <xsl:output indent="yes"/>
  
  <xsl:mode on-no-match="shallow-copy"/>
  
  <xsl:template match="resources">
      <xsl:copy>
          <xsl:for-each-group select="resource, $doc2/resources/resource" group-by="@name">
              <resource>
                   <xsl:attribute name="resourcename">
                        <xsl:value-of select="@name" />
                    </xsl:attribute>
                  <codes>
                      <xsl:for-each-group select="current-group()!codes/code" group-by="@id">
                          <xsl:apply-templates
                            select="if (current-group()[2] and ./@override = 'true')
                                    then current-group()[2]
                                    else ."/>
                      </xsl:for-each-group>
                  </codes>
              </resource>
          </xsl:for-each-group>
      </xsl:copy>
  </xsl:template>
  
</xsl:stylesheet>
